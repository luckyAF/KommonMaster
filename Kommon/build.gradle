apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


def siteUrl = 'https://github.com/luckyAF/KommonMaster' //项目在github主页地址
def gitUrl = 'https://github.com/luckyAF/KommonMaster.git'   //Git仓库的地址

group = "com.luckyaf"//发布aar前缀根节点

version = "0.0.9"//发布aar的库版本

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}



install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Kommon'//添加项目描述
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'luckyAf'//设置自己ID
                        name '项重飞'//设置自己名字
                        email 'luckyaf1993@gmail.com'//设置自己邮箱
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "Kommon" // 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字
        name = "Kommon" //项目在JCenter的名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.kotlinCoroutinesVersion"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlinCoroutinesVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$rootProject.kotlinVersion"
    compileOnly "com.android.support:appcompat-v7:$rootProject.androidSupportVsersion"
    compileOnly "com.android.support:cardview-v7:$rootProject.androidSupportVsersion"
    compileOnly "com.android.support:design:$rootProject.androidSupportVsersion"
    compileOnly "com.android.support:support-annotations:$rootProject.androidSupportVsersion"
    compileOnly "com.android.support:recyclerview-v7:$rootProject.androidSupportVsersion"
    compileOnly "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    compileOnly "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
    compileOnly "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    compileOnly "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    compileOnly "com.tbruyelle.rxpermissions2:rxpermissions:$rootProject.rxpermissions2Version"
    compileOnly "com.google.code.gson:gson:$rootProject.gsonVersion"
    compileOnly "com.squareup.okhttp3:okhttp:$rootProject.okHttpVersion"
    compileOnly "com.tencent:mmkv:$rootProject.mmkvVersion"
    compileOnly "org.greenrobot:eventbus:$rootProject.eventBusVersion"




}

//./gradlew bintrayUpload
//./gradlew clean build bintrayUpload
